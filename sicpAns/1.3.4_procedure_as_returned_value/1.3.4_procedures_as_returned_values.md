# 1.3.4 Procedures as Returned Values

## we need to think below questions after reading this section:
why we need a procedure as returned value?  
or  
what is the usage of procedure as returned value?

## reconstruct the square root procedure with average damping

```
; procedure as return value
(define (average-damp f)
  (lambda (x) (/ (+ x (f x)) 2))) 
; fixed point search function
(define tolerance 0.00001)
(define (fixed-point f first-guess)
  (define (close-enough? a b) (< (abs (- a b)) tolerance))
  (define (try guess)
    (let ((next (f guess)))
      (if(close-enough? guess next)
        next
        (try next))))
  (try first-guess))
; original implementation: inline lambda
(define (square-root x)
  (fixed-point (lambda (y) (/ (+ y (/ x y)) 2)) 
                             1.0))
; reconstruction with returned procedure
(define (square-root x)
  (fixed-point (average-damp (lambda (y) (/ x y)))) 
                             1.0))				 
```
From the above two implementations, we can see that:
- The later one is more describable as the named procedures are more clear for you to understand the logic.
- The later one is more general and is easy to extend. 
  - For example, extending above square-root as a cube-root procedure:

```
(define (cube-root x)
 (fixed-point (average-damp (lambda (y) (/ x (* y y)))) 
                            1.0))
	
```

## Newton's method
If x->g(x) is a differential function(可微分函数), then the solution to equation g(x)=0 is a fixed-point of the function x->f(x):  

$$ f(x) = x - \frac{g(x)}{Dg(x)} $$

### Derivative to a function g(x)

```
(define dx 0.00001)
(define (deriv g) 
  (lambda (x) (/ (- (g (+ x dx)) (g x)) dx)))
```
Like average-damp function, deriv function returns a procedure based on an input procedure g.

For example, to approximate the the derivative of x->x^3 at 5, we can do:
  (define (cube x) (* x x x))
  ((deriv cube) 5) = 75.00014999664018

### Implementation of Newton's method
With the help of deriv function, we can implement Newton's method as:

```
(define (newton-transform g)
  (lambda (x) (- x (/ (g x) ((deriv g) x)))))
(define (newton-method g guess) 
  (fixed-point (newton-transform g) guess))

(define (square-root y)
  (newton-method
    (lambda (x) (- (* x x) y))
    0.1))
```

### Abstractions and first-class procedures

```
; more general procedure template
(define (fixed-point-of-transform g transform guess)
  (fixed-point (transform g) guess))
  
; example of square-root implementation
(define (square-root x) 
  (fixed-point-of-transform 
    (lambda (y) (- (* y y) x))
    newton-transform
    1.0))
```
In Lisp, procedure is treated as first-class citizen status. The characteristic of First-Class means:
- They may be named by variables;
- They may be passed as arguments to procedures;
- They may be returned as the results of the procedure;
- They may be included in data structures.： 我们今天一起看看RC的

### Exercise 1.40: 
Define a procedure cubic that can be used together with the newtons-method procedure in expressions of the form

    (newtons-method (cubic a b c) 1)
to approximate zeros of the cubic

    x^3 + ax^2 + bx + c.	

```
(define (cube x) (* x x x))
(define (square x) (* x x))
(define (cubic a b c) (lambda (x) (+ (cube x) (* a (square x)) (* b x) c)))
(define (cubic-solution a b c) (newton-method (cubic a b c) 1))

(cubic-solution 1 1 1) = -0.9999999999997796
```
### Exercise 1.41: 
Define a procedure double that takes a procedure of one argument as argument and returns a procedure that applies the original procedure twice. For example, if inc is a procedure that adds 1 to its argument, then (double inc) should be a procedure that adds 2. What value is returned by  

  (((double (double double)) inc) 5)

```
; way #1 : procedure as returned value
(define (double f) (lambda (x) (f (f x))))
(define (inc x) (+ x 1))
((double inc) 2)  

; way #2  procedure as argument
(define (double f x) (f (f x)))
(define (inc x) (+ x 1))
(double inc 2) 	
```
### Exercise 1.42: 
Let f and g be two one-argument functions. The composition f and g is defined to be the function x -> f(g(x)). Define a procedure compose that implements composition.
For example, if inc is a procedure that adds 1 to its argument,
((compose square inc) 6) = 49

```
(define (compose f g) (lambda (x) (f (g x))))
(define (square x) (* x x))
(define (inc x) (+ x 1))
((compose square inc) 6)
```
### Exercise 1.43: 
If f is a numerical function and n is a positive integer, then we can form the nth repeated application of f , which is defined to be the function whose value at x is f (f (...... (f (x)) ...... )). For example, if f is the function x -> x + 1, then the nth repeated application of f is the
function x -> x +n. If f is the operation of squaring a number, then the nth repeated application of f is the function that raises its argument to the 2n-th power.   
Write a procedure that takes as inputs a procedure that computes f and a
positive integer n and returns the procedure that computes the nth repeated application of f . Your procedure should be able to be used as follows:  

((repeated square 2) 5) = 625

```
(define (repeated f n) 
  (define (repeated-iter g n)
    (if(> n 1)
      (repeated-iter (lambda (x) (f (g x))) (- n 1))
      g))
  (repeated-iter f n))

(define (square x) (* x x))
```

### Exercise 1.44 Smooth function compound with repeated function

```
(define (repeated f n)
  (define (repe-iter g i)
    (if (> i 1)
      (repe-iter (lambda (x dx) (f (g x dx) dx)) (- i 1)) 
      g))
  (repe-iter f n))
(define (smooth f) 
  (lambda (x dx) (/ (+ (f (- x dx)) 
                       (f x) 
                       (f (+ x dx))) 
                    3)))

(define (n-smooth f x dx n) 
  ((repeated (smooth f) n) x dx))

(define (square x) (* x x))

(n-smooth square 0 1.0 10)
```
### Exercise 1.45 Smooth function compound with repeated function

```
(define (i-expt b n)
  (define (iter-expt b n a k c)
    (if (= n 0)
        c
        (cond ((< (- n (* 2 k)) 0) (iter-expt b (- n k) b 1 (* a c))) 
              ((= (- n (* 2 k)) 0) (iter-expt b 0 b 0 (* a a c))) 
              (else (iter-expt b n (* a a) (* 2 k) c)))))
  (iter-expt b n b  1 1))

(define (repeated f n)
  (define (repe-iter g i)
    (if (> i 1)
      (repe-iter (lambda (x) (f (g x))) (- i 1)) ;(repe-iter (lambda (x) (g (f x))) (- i 1)) also works
      g))
  (repe-iter (lambda (x) (f x)) n))

(define (average-damp f)
  (lambda (y) (/ (+ y (f y)) 2)))

(define (fixed-point f first-guess)
  (define tolerance 0.00001)
  (define (close-enough? v1 v2)
    (< (abs (- v1 v2))
       tolerance))
  (define (try guess)
    (let ((next (f guess)))
      (if (close-enough? guess next)
        next
        (try next))))
  (try first-guess))
  
(define (nth-root x nth k)
  (fixed-point (repeated (average-damp (lambda (y) (/ x (i-expt y (- nth 1))))) k) 1))  
```
**running time**
## Running
```
(nth-root 2.0 2 1): 1.4142135623746899
(nth-root 2.0 2 2): 1.414213562373095
(nth-root 8 3 1): ctrl ^C ending
(nth-root 8.0 3 1): 1.9999981824788517
(nth-root 8.0 3 2): 2.0000009087630515
(nth-root 8.0 3 3): 1.9999995456190938
(nth-root 16.0 4 1): ctrl ^C ending
(nth-root 16.0 4 2): 1.9829851551723494
```

### Exercise 1.46 iterative-improve

```
(define (iterative-improve good-enough? improve)
  (lambda (guess)
    (if(good-enough? guess)
	  guess
	  ((iterative-improve good-enough? improve) (improve guess)))))
(define (sqrt2 x)
  (define (g? gs)
    (< (abs (- (square gs) x)) 0.001))
  (define (imp gs)
    (/ (+ gs (/ x gs)) 2.0))
  ((iterative-improve g? imp) 1))

(define (square x) (* x x))

(define (fixed-point2 imp first-guess)
  (define tolerance 0.00001)
  (define (g? gs)
    (< (abs (- gs (imp gs)))
       tolerance))
  ((iterative-improve g? imp) first-guess))
(define (sqrt3 x)
  (fixed-point2 (lambda (y) (/ (+ y (/ x y)) 2)) 1))
  
(sqrt2 4.0) = 2.0000000929222947
(sqrt2 9.0) = 3.00009155413138
(sqrt3 4.0) = 2.0000000929222947
(sqrt3 9.0) = 3.000000001396984

```

